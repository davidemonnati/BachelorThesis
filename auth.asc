SETG url: ("https://demo.thingsboard.io")
SETG header:({
    "Content-Type": "application/json"
})
SETL modelid: (-1)

PROCEDURE global GET_LOGGED_USER modelid: integer respstat: reference {
    SETL modelid: (modelid)
    CC "Core" GET_CLASS_ID classname:("__ModelTypeMetaData__")
    CC "Core" GET_ATTR_ID classid:(classid) attrname:("auth")
    CC "Core" GET_ATTR_VAL objid:(modelid) attrid:(attrid)
    SET token: (val)
    SETG header["Authorization"]: ("Bearer " + token)

    IF (token != "") {
        MAKE_GET_REQUEST path: ("api/auth/user") reqHeader: (header) respbodyToMap: response respstat: respstat
        IF (respstat = 401) {
            CC "AdoScript" ERRORBOX ("Please log in again") title:("Session expired") ok
            AUTH token: token
        } ELSE {
        }
    } ELSE {
        CC "AdoScript" INFOBOX ("User not logged on Thingsboard!") title:("Authentication failed")
        AUTH token: token
    }
    SETG header["Authorization"]: ("Bearer " + token) # Update header token
}

PROCEDURE global AUTH token: reference {
    SET req: (map())
    CC "AdoScript" EDITFIELD caption:("Enter mail") title:("Login: Mail")
    SET req["username"]: (text)
    CC "AdoScript" EDITFIELD caption:("Enter password") title:("Login: Password")
    SET req["password"]: (text)

    MAKE_POST_REQUEST path: ("api/auth/login") reqHeader: (header) reqbody: (STR req) mapRespBody: response respstat: respstat    

    IF (respstat = 401) {
        CC "AdoScript" ERRORBOX ("Authentication failed!\n\nInvalid username or password.") title:("Authentication failed") ok
        EXIT
    } ELSE {
        SET token: (response SUB "token")
        # Save token into auth attribute
        CC "Core" GET_CLASS_ID classname:("__ModelTypeMetaData__")
        CC "Core" GET_ATTR_ID classid:(classid) attrname:("auth")
        CC "Core" SET_ATTR_VAL objid:(modelid) attrid:(attrid) val:(token)
    }
}

PROCEDURE global MAKE_POST_REQUEST path: string reqHeader: map reqbody: string mapRespBody: reference respstat: reference {
    HTTP_SEND_REQUEST(url + "/" + path) str_method:("POST") map_reqheaders:(reqHeader) str_reqbody:(reqbody) val_respcode:respstat map_respheaders:respheaders str_respbody:respbody
    CC "AdoScript" INFOBOX (respstat)
    SET mapRespBody: (fromJson(respbody))
}

PROCEDURE global MAKE_GET_REQUEST path: string reqHeader: map respbodyToMap: reference respstat: reference {
    HTTP_SEND_REQUEST(url + "/" + path) str_method:("GET") map_reqheaders:(header) str_reqbody:("") val_respcode:respstat map_respheaders:respheaders str_respbody:respbody
    SET respbodyToMap: (fromJson(respbody))
}


