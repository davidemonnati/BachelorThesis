# Questo script ha come responsabilità quella di esportare i modelli ADOxx in Thingsboard.
# L'obiettivo è quello di far selezionare all'utente, tramite una MLISTBOX, quali modelli vuole esportare e riconoscere
# in automatico il tipo del modello e degli oggetti che ci sono all'interno.

EXECUTE file:("D:\\Universita\\Tesi\\ADOxx\\Script\\ExportLogicThingsBoard.asc")
EXECUTE file:("D:\\Universita\\Tesi\\ADOxx\\Script\\auth.asc")
EXECUTE file:("D:\\Universita\\Tesi\\ADOxx\\Script\\ExportScenarioThingsBoard.asc")

SELECT_MODELS

# Procedura che prende tutti i modelli attivi che sono presenti in ADOxx, filtra quelli che hanno
# come attributo Model Type = PSM e Platform = ThingsBoard e li visualizza in una MLISTBOX.
# Quando l'utente seleziona i modelli e clicca il bottone OK, viene avvata la procedura START_UPLOAD
PROCEDURE global SELECT_MODELS {
    SETL modelList: ""
    CC "Core" GET_MODELGROUP_ID mgroupname:"Models" sep:"\t"
    CC "Core" GET_MODELGROUP_MODELS mgroupid:(mgroupid) getversionids

    FOR id in:(modelids) {
        CC "Core" GET_ATTR_VAL objid:(VAL id) attrname:("Model Type") 
        SETL modelType: (val)
        CC "Core" GET_ATTR_VAL objid:(VAL id) attrname:("Platform")
        SETL platformModel: (val)
        IF(modelType = "PSM" AND platformModel = "ThingsBoard") {
            CC "Core" GET_MODEL_INFO modelid: (VAL id)
            SET modelList: (modelname + " " + modelList)
        }
    }

    CC "AdoScript" MLISTBOX entries: (modelList)
                            toksep:" "
                            title:"Upload models"
                            oktext:"Upload"
                            boxtext:"Choose your models"
    IF (endbutton = "ok") {
        START_UPLOAD modelList: (selection)
    }
}

# La procedura START_UPLOAD riceve in input la lista dei modelli che sono stati selezionati dalla MLISTBOX
# e per ognuno controlla se si tratta di un modello di tipo logic o di un modello di tipo scenario.

PROCEDURE global START_UPLOAD
    modelList: string # Lista dei modelli che sono stati selezionati su SELECT_MODELS
{
    FOR modelName in:(modelList) {
        CC "Core" GET_MODEL_ID modelname:(modelName) modeltype:("IoT Scenario")
        SET modelScenarioID: (modelid)
        IF (modelid <= 0) {
            CC "Core" GET_MODEL_ID modelname:(modelName) modeltype:("IoT Logic")
            SET modelLogicID: (modelid)
        }
        CC "Core" GET_MODEL_BASENAME modelid:(modelid)

        ## Controllo in che tipo di modello mi trovo.
        CC "Core" GET_MODEL_MODELTYPE modelid:(modelid)
        GET_LOGGED_USER modelid: (modelid) respstat: status
        IF (status = 200) {
            #CC "AdoScript" MSGWIN ("Uploading to Thingsboard")
            IF(modeltype = "IoT Logic") {
                LOGIC
            }
            
            IF(modeltype = "IoT Scenario") {
                SCENARIO
            }
            #CC "AdoScript" MSGWIN hide
        }
    }
}

# Converte una lista di elementi array
PROCEDURE global CONVERT_LIST_TOARRAY
    input: string # lista di elementi in formato stringa
    output: reference # array con gli elementi della lista
{
    SET output: ({})
    FOR element in:(input) {
        SET dummy: (aappend(output, element))
    }
}

# Procedura che serve ad inserire un nuovo record all'interno di una tabella
PROCEDURE global ADD_TABLE_RECORD objid: integer classname: string tableName: string recAttr: string recVal: string  {
    CC "Core" GET_CLASS_ID classname:(classname) # classid è il valore che ritorna GET_CLASS_ID
    CC "Core" GET_ATTR_ID classid:(classid) attrname:(tableName)
    EMPTY_TABLE attrid: (attrid) objid: (objid) # Elimino tutte le righe della tabella prima di inserire il nuovo id di thingsboard
    CC "Core" ADD_REC_ROW objid:(objid) attrid:(attrid) # attrid è il valore che ritorna GET_ATTR_ID clid è il valore che ritorna GET_CLASS_ID 
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:(recAttr) # rowid è il valore che ritorna ADD_REC_ROW
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:(recVal) # rowid è il valore che ritorna ADD_REC_ROW
}

# Procedura che serve ad eliminare tutti i record di una tabella
PROCEDURE global EMPTY_TABLE attrid: integer objid: integer { 
    CC "Core" GET_ALL_REC_ATTR_ROW_IDS objid:(objid) attrid:(attrid)
    FOR row in:(rowids) {
        CC "Core" REMOVE_REC_ROW objid:(objid) attrid:(attrid) rowid:(VAL row)
    }
}

