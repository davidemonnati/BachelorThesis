# La responsabilità di questo script è quella di generare un modello importando gli oggetti da thingsboard.
# Ho considerato l'asset come l'oggetto che l'utente può scegliere di importare, quindi la prima operazione che viene
# effettuata è quella di prendere la lista di tutti gli asset da thingsboard e mostrarla all'interno di una LISTBOX,
# dove l'utente può scegliere cosa importare.
# Una volta scelto l'asset da importare lo script si occuperà in automatico di prendere tutti gli oggetti ad esso collegati
# e di inserirli all'interno del modello di tipo scenario.
# Il device profile contiene come attributo l'ID della rule chain e di conseguenza il modello logico verrà importato automaticamente.
# Infine, è compito dell'utente decidere se importare anche la dashboard e nel caso effettuare i collegamenti con gli attributi
# a cui i widget fanno riferimento.

EXECUTE file:("D:\\Universita\\Tesi\\ADOxx\\Script\\auth.asc")
EXECUTE file:("D:\\Universita\\Tesi\\ADOxx\\Script\\ImportRuleChain.asc")

SEND "GET_ACTIVE_MODEL" to:"Modeling" answer:modelid
SETL newModelID: (VAL modelid)

IF (modelid = "") {
    CC "AdoScript" ERRORBOX "Open a blank model first!"
    EXIT
} ELSE {
    CC "Core" GET_MODEL_MODELTYPE modelid:(VAL modelid)
    CC "Core" GET_ATTR_VAL objid:(VAL modelid) attrname:("Model Type") 
    IF(val = "PIM") {
        CC "AdoScript" ERRORBOX "Select a PSM model first!"
        EXIT
    } ELSIF (modeltype != "IoT Scenario") {
        CC "AdoScript" ERRORBOX "Select IoT scenario model first!"
        EXIT
    }
}

CC "Core" GET_CLASS_ID classname:("hasAttribute")
SET attributeRelationID: (classid)
CC "Core" GET_CLASS_ID classname:("hasService")
SET serviceRelationID: (classid)
CC "Core" GET_CLASS_ID classname:("hasDevice")
SET deviceRelationID: (classid)
CC "Core" GET_CLASS_ID classname:("hasLocation")
SET locationRelationID: (classid)
CC "Core" GET_CLASS_ID classname:("hasWidget")
SET widgetRelationID: (classid)

GET_LOGGED_USER modelid: (newModelID) respstat: status
IF (status = 200) {
    GET_ALL_ASSET_TYPE
    GET_DASHBOARD
    CC "Core" SAVE_MODEL modelid:(newModelID) update-sys-attrs:1
}

PROCEDURE global GET_ALL_ASSET_TYPE {
    CC "AdoScript" MSGWIN ("Get asset list...")
    SET assetListName: ""
    SET assetMap: (map())
    MAKE_GET_REQUEST path: ("api/asset/types") reqHeader: (header) respbodyToMap: response respstat: respstat
    FOR i from:0 to:((response.length)-1) {
        MAKE_GET_REQUEST path: ("api/tenant/assets?type=" + (response[i] SUB "type") + "&pageSize=9999&page=0") reqHeader: (header) respbodyToMap: assets respstat: respstat
        SET assetData: (assets SUB "data")
        FOR j from:0 to:((assetData.length)-1) {
            SET assetListName: (assetListName + assetData[j] SUB "name" + ";")
            SET assetMap[assetData[j] SUB "name"]: (assetData[j])
        }
    }
    CC "AdoScript" MSGWIN hide
    CC "AdoScript" LISTBOX  entries: (assetListName)
            toksep:";"
            title:"Select asset do download!"
            oktext:"Download"
            boxtext:"Select asset"
            selection:"Cancel"
    IF (endbutton = "ok") {
        CC "AdoScript" MSGWIN ("Get all objects...")
        GET_SYSTEM selectedAsset: (assetMap SUB selection)
        CC "AdoScript" MSGWIN hide
    }
    CC "Core" SAVE_MODEL modelid: (newModelID)
}

PROCEDURE global GET_SYSTEM selectedAsset: map {
    SET classname: "system"
    SET additionalInfo: (selectedAsset SUB "additionalInfo")
    CC "Core" GET_CLASS_ID classname:(classname)
    CC "Core" CREATE_OBJ modelid:(newModelID) classid:(classid) objname:(selectedAsset SUB "name") # return objid
    SET assetID: (objid)

    CC "Core" SET_ATTR_VAL objid:(objid) attrname:("name") val:(selectedAsset SUB "name")
    CC "Core" SET_ATTR_VAL objid:(objid) attrname:("description") val:(additionalInfo SUB "description")

    # Save records
    # Get label
    CC "Core" GET_CLASS_ID classname:(classname)
    CC "Core" GET_ATTR_ID classid:(classid) attrname:("specification")
    CC "Core" ADD_REC_ROW objid:(objid) attrid:(attrid)
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:("label")
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:(selectedAsset SUB "label")
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Type" val:("String")
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Description" val:("Label of the asset")  

    # Get type
    CC "Core" GET_CLASS_ID classname:(classname)
    CC "Core" GET_ATTR_ID classid:(classid) attrname:("specification")
    CC "Core" ADD_REC_ROW objid:(objid) attrid:(attrid)
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:("type")
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:(selectedAsset SUB "type")
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Type" val:("String")
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Description" val:("Type of the asset")

    # Get Thingsboard id
    SET id: (selectedAsset SUB "id")
    CC "Core" GET_CLASS_ID classname:(classname)
    CC "Core" GET_ATTR_ID classid:(classid) attrname:("platformData")
    CC "Core" ADD_REC_ROW objid:(objid) attrid:(attrid)
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:("tb_object_id")
    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:(id SUB "id")

    # Check attributes
    GET_ATTRIBUTES fromTbId: (id SUB "id") fromAdoxxObjId: (assetID) objType: ("ASSET")
    GET_TELEMETRY fromTbId: ((id SUB "id")) fromAdoxxObjId: (assetID) objType: ("ASSET")

    # Get devices
    MAKE_GET_REQUEST path: ("api/relations/info?fromId=" + (id SUB "id") + "&fromType=ASSET") reqHeader: (header) respbodyToMap: response respstat: respstat
    SET deviceIdList: ({})
    FOR i from:0 to:((response.length)-1) {
        SET to: (response[i] SUB "to") # (to SUB "id")
        SET dummy: (aappend(deviceIdList, (to SUB "id")))
    }
    GET_CONNECTED_DEVICES deviceIdList: (deviceIdList)
}

PROCEDURE global GET_ATTRIBUTES fromTbId: string fromAdoxxObjId: integer objType: string {
    MAKE_GET_REQUEST path: ("api/plugins/telemetry/" + objType + "/" + fromTbId + "/values/attributes") reqHeader: (header) respbodyToMap: response respstat: respstat
    FOR i from:0 to:((response.length)-1) {
        SET value: (response[i] SUB "value")
        IF (type(value) != "string") {
            GET_PROPERTY key: (response[i] SUB "key") value: (STR (response[i] SUB "value")) fromAdoxxObjId: (fromAdoxxObjId)
        } ELSE {
            GET_PROPERTY key: (response[i] SUB "key") value: (response[i] SUB "value") fromAdoxxObjId: (fromAdoxxObjId)
        }
    }
}

PROCEDURE global GET_TELEMETRY fromTbId: string fromAdoxxObjId: integer objType: string {
    MAKE_GET_REQUEST path: ("api/plugins/telemetry/" + objType + "/" + fromTbId + "/values/timeseries") reqHeader: (header) respbodyToMap: response respstat: respstat
    SET keys: (mapKeysArray(response))
    FOR i from:0 to:((keys.length)-1) {
        SET value: ((response SUB keys[i])[0] SUB "value")
        IF (type(value) != "string") {
            GET_PROPERTY key: (keys[i]) value: (STR(value)) fromAdoxxObjId: (fromAdoxxObjId)
        } ELSE {
            GET_PROPERTY key: (keys[i]) value: (value) fromAdoxxObjId: (fromAdoxxObjId)
        }
    }
}

PROCEDURE global GET_PROPERTY key: string value: string fromAdoxxObjId: integer {
    SET classname: "attribute"
    CC "Core" GET_CLASS_ID classname:(classname)
    SET randomNumber: (randomDiscreteUniformDist(1000, 9999))
    SET attrName: (key + "-" + STR randomNumber)
    CC "Core" CREATE_OBJ modelid:(newModelID) classid:(classid) objname:(attrName)
    CC "Core" SET_ATTR_VAL objid:(objid) attrname:("name") val:(key)
    CC "Core" SET_ATTR_VAL objid:(objid) attrname:("type") val:(type(value)) # is always string
    CC "Core" SET_ATTR_VAL objid:(objid) attrname:("value") val:(value)
    CC "Core" CREATE_CONNECTOR modelid:(newModelID) fromobjid:(fromAdoxxObjId) toobjid:(objid) classid:(attributeRelationID) 
}

PROCEDURE global GET_CONNECTED_DEVICES deviceIdList: array {
    FOR i from:0 to:((deviceIdList.length)-1) {
        MAKE_GET_REQUEST path: ("api/device/" + deviceIdList[i]) reqHeader: (header) respbodyToMap: response respstat: respstat
        SET deviceProfileId: (response SUB "deviceProfileId")

        SET additionalInfo: (response SUB "additionalInfo")
        CC "Core" GET_CLASS_ID classname:("device")
        CC "Core" CREATE_OBJ modelid:(newModelID) classid:(classid) objname:(response SUB "name") # return objid
        SET deviceID: (objid)
        CC "Core" SET_ATTR_VAL objid:(deviceID) attrname:("name") val:(response SUB "name")
        CC "Core" SET_ATTR_VAL objid:(deviceID) attrname:("description") val:(additionalInfo SUB "description")

        # Save records
        # is_gateway
        SET additionalInfo: (response SUB "additionalInfo")
        CC "Core" GET_ATTR_ID classid:(classid) attrname:("specification")
        CC "Core" ADD_REC_ROW objid:(deviceID) attrid:(attrid)
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:("is_gateway")
        IF ((additionalInfo SUB "gateway") = 0) {
            CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:("false")
        } ELSE {
            CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:("true")
        }
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Type" val:("Boolean")
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Description" val:("If this device is a gateway")

        # label
        SET additionalInfo: (response SUB "additionalInfo")
        CC "Core" GET_ATTR_ID classid:(classid) attrname:("specification")
        CC "Core" ADD_REC_ROW objid:(deviceID) attrid:(attrid)
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:("label")
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:(response SUB "label")
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Type" val:("String")
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Description" val:("Label of the device")

        # Get Thingsboard id
        SET id: (response SUB "id")
        CC "Core" GET_ATTR_ID classid:(classid) attrname:("platformData")
        CC "Core" ADD_REC_ROW objid:(deviceID) attrid:(attrid)
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:("tb_object_id")
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:(id SUB "id")

        # Create connector system -> device
        CC "Core" CREATE_CONNECTOR modelid:(newModelID) fromobjid:(assetID) toobjid:(deviceID) classid:(deviceRelationID)

        # Get device profile
        GET_DEVICE_PROFILE deviceProfileId: (deviceProfileId SUB "id") deviceAdoxxId: (deviceID)

        # Get attributes
        GET_ATTRIBUTES fromTbId: (deviceIdList[i]) fromAdoxxObjId: (deviceID) objType: ("DEVICE")
        GET_TELEMETRY fromTbId: (deviceIdList[i]) fromAdoxxObjId: (deviceID) objType: ("DEVICE")
    }
}

PROCEDURE global GET_DEVICE_PROFILE deviceProfileId: string deviceAdoxxId: integer {
    MAKE_GET_REQUEST path: ("api/deviceProfile/" + deviceProfileId) reqHeader: (header) respbodyToMap: response respstat: respstat
    SET id: (response SUB "id")

    # Before creating check if exist
    CC "Core" GET_CLASS_ID classname:("service")
    CC "Core" GET_OBJ_ID modelid:(newModelID) classid:(classid) objname:(response SUB "name")
    SET serviceID: (objid)

    # Prima di creare il service controllo se già esiste.
    # GET_OBJ_ID ritorna 0 se l'oggetto non esiste, altrimenti ritorna -1.
    IF (serviceID = -1) {
        # Create il service
        CC "Core" GET_CLASS_ID classname:("service")
        CC "Core" CREATE_OBJ modelid:(newModelID) classid:(classid) objname:(response SUB "name") # return objid
        SET serviceID: (objid)
        CC "Core" SET_ATTR_VAL objid:(objid) attrname:("name") val:(response SUB "name")
        CC "Core" SET_ATTR_VAL objid:(objid) attrname:("description") val:(response SUB "description")
        SET protocol: (response SUB "transportType")
        IF (protocol != "MQTT") {
            CC "Core" SET_ATTR_VAL objid:(objid) attrname:("protocol") val:("DEFAULT")
        } ELSE {
            CC "Core" SET_ATTR_VAL objid:(objid) attrname:("protocol") val:("MQTT")
        }

        # Get Thingsboard id
        CC "Core" GET_ATTR_ID classid:(classid) attrname:("platformData")
        CC "Core" ADD_REC_ROW objid:(objid) attrid:(attrid)
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:("tb_object_id")
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:(id SUB "id")

        # Get and set rule chain
        # Devo controllare se è uguale a map perché nel caso in cui il device profile non ha alcuna rule chain associata,
        # la key defaultRuleChainId del JSON di risposta è uguale a null, quindi di tipo string, altrimenti contiene una map
        # con l'id della rule chain.
        IF(type(response SUB "defaultRuleChainId") = "map") {
            SET ruleChainTbID: ((response SUB "defaultRuleChainId") SUB "id")
            IMPORT_RULE_CHAIN auth: (header SUB "Authorization") rulechainTbID: (ruleChainTbID) serviceID: (serviceID)
        }
    } ELSE {
        # Make only relation
    }
    
    # Create connector device -> service
    CC "Core" CREATE_CONNECTOR modelid:(newModelID) fromobjid:(deviceAdoxxId) toobjid:(serviceID) classid:(serviceRelationID)
}

PROCEDURE global GET_DASHBOARD {
    CC "AdoScript" EDITFIELD caption:("Enter the thingsboard id of dashboard") title:("Import dashboard")

    SET dashboardTbId: (text)
    CC "Core" GET_CLASS_ID classname:("widget")
    IF (type(dashboardTbId) != "undefined") {
        #CC "AdoScript" MSGWIN ("Get dashboard...")
        GET_SPECIFICATIONS platform:("ThingsBoard") specs:specs # Read specification file
        MAKE_GET_REQUEST path: ("api/dashboard/" + dashboardTbId) reqHeader: (header) respbodyToMap: response respstat: respstat
        SET widgets: ((response SUB "configuration") SUB "widgets") # prendo i widget dal JSON ricevuto da thingsboard
        SET keysArray: (mapKeysArray(widgets)) # creo array con gli id dei widget
        FOR i from:0 to:((keysArray.length)-1) { # Ogni widget lo creo sul modello adoxx
            SET widget: (widgets SUB keysArray[i])
            SET config: (widget SUB "config")
            CC "Core" CREATE_OBJ modelid:(newModelID) classid:(classid) objname:(config SUB "title")
            CC "Core" SET_ATTR_VAL objid:(objid) attrname:("name") val:(config SUB "title")
            CC "Core" SET_ATTR_VAL objid:(objid) attrname:("description") val:(widget SUB "description")

            CC "Core" GET_ATTR_ID classid:(classid) attrname:("specification")
            SETL specificationId: (attrid)

            # Aggiungo gli attributi thingsboard del widget sulla tabella specification
            SET tbType:(widget SUB "bundleAlias")
            IF (tbType = "maps_v2") {
                SET psmType: ("map")
                SET widgetSpecs: (((specs SUB "widget")[0] SUB psmType) SUB "specs")
                SET specsArray: (mapKeysArray(widgetSpecs))
                
                FOR j from:0 to:((specsArray.length)-1) {
                    SET keys: (widgetSpecs SUB specsArray[j])
                    CC "Core" ADD_REC_ROW objid:(objid) attrid:(specificationId)
                    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:(specsArray[j])
                    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Type" val:(keys SUB "type")
                    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Description" val:(keys SUB "description")
                }
            } ELSIF(tbType = "charts") {
                SET psmType: ("line_chart")
                SET widgetSpecs: (((specs SUB "widget")[0] SUB psmType) SUB "specs")
                SET specsArray: (mapKeysArray(widgetSpecs))
                FOR j from:0 to:((specsArray.length)-1) {
                    SET keys: (widgetSpecs SUB specsArray[j])

                    CC "Core" ADD_REC_ROW objid:(objid) attrid:(specificationId)
                    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:(specsArray[j])
                    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Type" val:(keys SUB "type")
                    CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Description" val:(keys SUB "description")
                }
            } ELSE {
                # Se non è ne di tipo maps ne di tipo charts non faccio nulla per ora
            }
            CC "Core" SET_ATTR_VAL objid:(objid) attrname:("psm_type") val:(psmType)
        }

        #CC "AdoScript" MSGWIN hide
    } ELSE {
        CC "AdoScript" INFOBOX ("The dashboard will not be imported.")
    }
}


