PROCEDURE global IMPORT_RULE_CHAIN auth: string rulechainTbID: string serviceID: integer {
    GET_SPECIFICATIONS platform: ("ThingsBoard") specs: specifications

    CC "Core" GET_MODELGROUP_ID mgroupname:("Models") sep:("~")
    CC "Core" CREATE_MODEL modeltype:("IoT Logic") modelname:("temp_logic") version:"" mgroups:(mgroupid)
    SET toDelete: (modelid)
    CC "Core" LOAD_MODEL modelid:(modelid)
    PIM_TO_PSM platform:("ThingsBoard") specs: (specifications) modelID: (toDelete) targetID: targetID
    CC "Core" DELETE_MODEL modelid:(toDelete)

    # Save authorization token into the model auth attribute
    CC "Core" GET_CLASS_ID classname:("__ModelTypeMetaData__")
    CC "Core" GET_ATTR_ID classid:(classid) attrname:("auth")
    CC "Core" SET_ATTR_VAL objid:(targetID) attrid:(attrid) val:(replall(auth, "Bearer ", ""))

    # Save Thingsboard id in the attribute of model
    CC "Core" GET_ATTR_ID classid:(classid) attrname:("Thingsboard id")
    CC "Core" SET_ATTR_VAL objid:(targetID) attrid:(attrid) val:(rulechainTbID)

    GET_RC_NODES rulechainTbID:(rulechainTbID) specifications: (specifications) modelid: (targetID) serviceID: (serviceID)
    CC "Core" SAVE_MODEL modelid:(targetID) update-sys-attrs:1
    CC "Modeling" CLOSE modelid: (targetID) quiet close
}

PROCEDURE global GET_RC_NODES rulechainTbID: string specifications: map modelid: integer serviceID: integer {
    # Creazione nodo data_stream_in
    CC "Core" GET_CLASS_ID classname:("data_stream_in")
    CC "Core" CREATE_OBJ modelid:(modelid) classid:(classid) objname:("Input")
    SET dataStreamID: (objid)
    CC "Core" SET_ATTR_VAL objid:(objid) attrname:("name") val:("Input")

    # Setto il valore dell'attributo service di tipo INTERREF 
    CC "Core" GET_ATTR_ID classid:(classid) attrname:("service")
    CC "Core" ADD_INTERREF attrid: (attrid) tmodelid:(modelid) objid: (dataStreamID) tobjid: (serviceID)

    MAKE_GET_REQUEST path: ("api/ruleChain/" + rulechainTbID + "/metadata") reqHeader: (header) respbodyToMap: response respstat: respstat
    SET connections: ((response SUB "connections"))
    SET nodes: (response SUB "nodes")
    FOR i from:0 to:((nodes.length)-1) {
        SEARCH_OBJ_TYPE specifications: (specifications) toSearch:(nodes[i] SUB "type") sObj: newObj sType: newType
        SET classname: (newObj)
        CC "Core" GET_CLASS_ID classname:(classname)
        CC "Core" CREATE_OBJ modelid:(modelid) classid:(classid) objname:(nodes[i] SUB "name")
        CC "Core" SET_ATTR_VAL objid:(objid) attrname:("name") val:(nodes[i] SUB "name")
        CC "Core" SET_ATTR_VAL objid:(objid) attrname:("description") val:((nodes[i] SUB "additionalInfo") SUB "description")

        # Salvo index nella tabella platformData
        CC "Core" GET_ATTR_ID classid:(classid) attrname:("platformData")
        CC "Core" ADD_REC_ROW objid:(objid) attrid:(attrid)
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Attribute" val:("index")
        CC "Core" SET_ATTR_VAL objid:(rowid) attrname:"Value" val:(i)
    }

    # Creo i collegamenti tra i vari nodi
    CC "Core" GET_CLASS_ID classname:("contains")
    SET containsRelationID: (classid)

    # Questa Ã¨ una stringa che devo trasformare in array
    CC "Core" GET_ALL_OBJS modelid:(modelid) #objids
    CONVERT_LIST_TOARRAY input: (objids) output: objsArray
    CC "Core" CREATE_CONNECTOR modelid:(modelid) fromobjid:(dataStreamID) toobjid:(VAL objsArray[1]) classid:(containsRelationID)

    FOR i from:0 to:((connections.length)-1) {
        SET tmpFrom: (connections[i] SUB "fromIndex")
        SET tmpTo: (connections[i] SUB "toIndex")
        SET fromIndex: (objsArray[tmpFrom + 1])
        SET toIndex: (objsArray[tmpTo + 1])
        CC "Core" CREATE_CONNECTOR modelid:(modelid) fromobjid:(VAL fromIndex) toobjid:(VAL toIndex) classid:(containsRelationID)
        CC "Core" SET_ATTR_VAL objid:(objid) attrname:("output") val:(connections[i] SUB "type")
    }
}

PROCEDURE global SEARCH_OBJ_TYPE specifications: map toSearch: string sObj: reference sType: reference {
    SET sType: ("")
    SET sObj: ("")
    FOR obj in:(mapKeysList(specifications)) sep:("~") {
        IF(type(specifications SUB obj) = "array") {
            FOR type in:(mapKeysList((specifications SUB obj)[0])) sep:("~") {
                IF ((((specifications SUB obj)[0] SUB type SUB "config") SUB "type") = toSearch) {
                    SET sType: (type)
                    SET sObj: (obj)
                }
            }
        }
    }
}

